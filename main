'Set 4 WS2812B displays with GPIO15 as input
gosub Loadsettings   'Load the saved settings
NeoScroll.Setup 2,15,1
'Set the first message as the ipaddress
NeoScroll.print  WORD$(IP$,1)+"  "+WORD$(IP$,1)+"  "+WORD$(IP$,1)
NeoScroll.colors scrlclr$
'then show the saved message
NEOSCROLL.NEXT scrlmsg$
NEOSCROLL.NEXTCOLORS scrlclr$


gosub showhtml  'show webpage
onhtmlreload showhtml  'if htmlreload
onhtmlchange showhtml  'if htmlchanges
'Set the refresh rate of the display (100 msec) - lower values -> scroll faster
timer0 100, scrollme
wait
end

Scrollme:  'scroll the display
 NeoScroll.scroll brightness
return

showhtml:    'create and show the webpage
a$ = "Recycle Bin <br>"
a$ = a$ +"<style>body{background-color: #93b874;}</style>"  'set background color for page
a$ = a$ + "Message "+textbox$(scrlmsg$,"msgid") + "<br>"
a$ = a$ + "colors are Blue Green Cyan Red Magenta Yellow White <br>"
a$ = a$ + "color codes "+textbox$(scrlclr$,"scrlid")+"<br>"
a$ = a$ + "Scroll Speed "+slider$(scrldelay,30,300,10)+str$(scrldelay)+"<br>"
a$ = a$ + "Brightness "+slider$(brightness,20,255,5)+str$(brightness)+"<br>"
a$ = a$ + button$("Save ",newmsg)+"<br>"
cls
HTML a$
timer0  scrldelay,scrollme    'update the scroll speed if it has been changed.
return

newmsg:    'if save button pressed
refresh    'sync the variables from webpage and the code
config$ = ""
WORD.SETPARAM config$, "scrlmsg", scrlmsg$
WORD.SETPARAM config$, "scrlclr", scrlclr$
WORD.SETPARAM config$, "scrldelay", str$(scrldelay)
WORD.SETPARAM config$, "brightness", str$(brightness)
print config$  'show what we are saving.
file.save ""sign.ini"",config$
wlog "Saving"
'update the message we scroll
NEOSCROLL.NEXT scrlmsg$
NEOSCROLL.NEXTCOLORS scrlclr$
return


Loadsettings:
wlog "Load in settings"
'creat default values if this is a first run.
config$ =""
WORD.SETPARAM config$, "scrlmsg", "Recycle Here !!!  "
WORD.SETPARAM config$, "scrlclr", "RGBYMCRGBYMCRGBYMC"
WORD.SETPARAM config$, "scrldelay", "50"
WORD.SETPARAM config$, "brightness", "55"

'check if clock.ini file exists
if FILE.EXISTS("sign.ini") = 0 then
 file.save "sign.ini",config$     'create file if not there.
 else
 config$ = file.read$("sign.ini")
 print "Read Config file"
 print config$
end if

'Read the settings
scrlmsg$ = WORD.GETPARAM$(config$,"scrlmsg")
scrlclr$ = WORD.GETPARAM$(config$,"scrlclr")
scrldelay = val(WORD.GETPARAM$(config$,"scrldelay"))
brightness = val(WORD.GETPARAM$(config$,"brightness"))

return



